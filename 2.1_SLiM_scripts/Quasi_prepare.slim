initialize() {
  defineConstant("Ne",50000);
  defineConstant("P_ben_max",0.25);
  defineConstant("rep",7);
  defineConstant("Name","quasi_pop"+Ne);
  defineConstant("Root","../Simulation_log");
  initializeMutationType("m1", 0.5, "f", 0.0); 
  initializeMutationType("m0", 0.5, "f", 0.0); 
  initializeGenomicElementType("g1", c(m1,m0), c(0.9,0.1));
  initializeGenomicElement(g1, 0, 159999);
  initializeMutationRate(1e-7);
  rates = c();
  ends = c();
  for (i in 1:15){
    ends = c(ends, i*10000-1,i*10000);
    rates = c(rates, 1e-4 , 0.5);
  }
  rates = c(rates, 1e-4);
  ends = c(ends, 16*10000-1);
  initializeRecombinationRate(rates, ends);
  m0.convertToSubstitution = T;
  m1.convertToSubstitution = T;
}

1 early() {
  sim.addSubpop("p1", Ne);
  p1.setValue('env_idx',0);
  harshness = max(rnorm(1,1,0.15),0.0);
  p1.setValue('harshness',harshness);
}

early() {
  if( sim.generation % 1000 == 0)
    print(sim.generation);

  if( runif(1) < 1/50){
    muts = sim.mutationsOfType(m1);
    env_idx = p1.getValue('env_idx');
    env_idx = env_idx+1; 
    p1.setValue('env_idx',env_idx);
    harshness = max(rnorm(1,1,0.15), 0.0);
    p1.setValue('harshness',harshness);
    for (mut in muts){
      beneficial_P = mut.getValue('P_ben');
      selcoeff_size = mut.getValue('selcoeff_size');
      if (selcoeff_size == 0) // neutral mutation
        next;
      selcoeff_size_env = selcoeff_size * harshness;
      selcoeff = rgamma(1,selcoeff_size_env,10);
      if (runif(1) > beneficial_P)
        selcoeff = -selcoeff;
      mut.setSelectionCoeff(selcoeff);
    }
  }
} 

mutation(m1){
  P_ben_0 = 1.0-0.04/P_ben_max;  // so that mean P_ben = 0.02
  if ( runif(1) < P_ben_0)
    beneficial_P = 0;
  else
    beneficial_P = runif(1, min=0, max=P_ben_max);
  mut.setValue('P_ben', beneficial_P);

  selcoeff_size = rexp(1,mu=0.01);
  harshness = p1.getValue('harshness');
  selcoeff_size_env = selcoeff_size * harshness;
  selcoeff = rgamma(1,selcoeff_size_env,10);  
  if (runif(1) > beneficial_P){  // so that P ~ 0.02 when fitness=1
    selcoeff = -selcoeff;
  }
  mut.setValue('selcoeff_size', selcoeff_size);
  mut.setSelectionCoeff(selcoeff);
  return mut;
}

mutation(m0){
  mut.setValue('selcoeff_size', 0);
  mut.setValue('P_ben',0);
  mut.setMutationType(m1);
  return mut;
}

late() {
/*
  muts = sim.mutations;
  Freqs = sim.mutationFrequencies(p1,muts);
  for (i in 0:(muts.length()-1)){
    mut = muts[i];
    if (mut.getValue('selcoeff_size') != 0)
      mut.appendKeysAndValuesFrom(Dictionary('AF_history',Freqs[i], 'selcoeff_history', mut.selectionCoeff));
  }
*/
}

200000 late() {
  sim.simulationFinished();
  sim.outputFull('./popstate_rep' +rep+ '.txt');
  
  i = 0;
  lines = 'ID,selcoeff_size,P_ben'; 
  muts = sim.mutations;
  for (mut in muts){
    selcoeff_size = mut.getValue('selcoeff_size');
    P_ben = mut.getValue('P_ben');
    lines = c(lines, paste(mut.id, selcoeff_size, P_ben,sep=','));
    i = i+1;
  }

  writeFile("./selcoeff_size_rep" +rep+ ".txt", lines);
}
